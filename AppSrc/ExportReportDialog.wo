Use cWebModalDialog.pkg
Use cWebPanel.pkg
Use cWebForm.pkg
Use cWebGroup.pkg
Use cWebMenuGroup.pkg
Use cWebMenuItem.pkg
Use cWebCheckbox.pkg
Use cWebLabel.pkg
Use cWebCombo.pkg
Use cWebButton.pkg
Use NameValuePairFunctions.pkg
Use cWebFileUploadForm.pkg

Use DRLanguage.Inc

Register_Function ReportExportType Returns Integer

Object oExportReportDialog is a cWebModalDialog
    Set psCaption to C_$DRExport
    Set piWidth to 800
    Set piColumnCount to 12

    { DesignTime = False }
    { WebProperty = Server }
    Property tNameValuePair[] pReportExportOptions

    Procedure CopyExportOptions Handle hoReport
        Boolean bOption
        tNameValuePair[] ReportExportOptions

        Get pbShowExportToCSV of hoReport to bOption
        Get NamedValueAdd ReportExportOptions C_$DRCSV bOption to ReportExportOptions

        Get pbShowExportToHTML of hoReport to bOption
        Get NamedValueAdd ReportExportOptions C_$DRHTML bOption to ReportExportOptions

        Get pbShowExportToGIF of hoReport to bOption
        Get NamedValueAdd ReportExportOptions C_$DRGIF bOption to ReportExportOptions

        Get pbShowExportToJPEG of hoReport to bOption
        Get NamedValueAdd ReportExportOptions C_$DRJPEG bOption to ReportExportOptions

        Get pbShowExportToPNG of hoReport to bOption
        Get NamedValueAdd ReportExportOptions C_$DRPNG bOption to ReportExportOptions

        Get pbShowExportToPDF of hoReport to bOption
        Get NamedValueAdd ReportExportOptions C_$DRPDF bOption to ReportExportOptions

        Get pbShowExportToRTF of hoReport to bOption
        Get NamedValueAdd ReportExportOptions C_$DRRTF bOption to ReportExportOptions

        Get pbShowExportToTIFF of hoReport to bOption
        Get NamedValueAdd ReportExportOptions C_$DRTIFF bOption to ReportExportOptions

        Get pbShowExportToXLS of hoReport to bOption
        Get NamedValueAdd ReportExportOptions C_$DRXLS2003 bOption to ReportExportOptions

        Get pbShowExportToXLSX of hoReport to bOption
        Get NamedValueAdd ReportExportOptions C_$DRXLS2007 bOption to ReportExportOptions

        WebSet pReportExportOptions to ReportExportOptions
    End_Procedure

    Object oWebMainPanel is a cWebPanel
        Set piColumnCount to 12

        Object oExplanation is a cWebLabel
            Set piColumnSpan to 0
            Set psCaption to C_$DRExportExplanation
        End_Object

        Object oOutputCombo is a cWebCombo
            Set psLabel to C_$DRExportTo
            Set peLabelAlign to alignRight
            Set piColumnSpan to 5
            Set pbServerOnChange to True

            Procedure OnChange String sNewValue String sOldValue
                // Compare values MUST be hardcoded!
                WebSet pbRender of oOwnerPasswordForm to (sNewValue = "PDF")
                WebSet pbRender of oUserPasswordForm to (sNewValue = "PDF")
                WebSet pbRender of oPageModeCombo to (sNewValue = "PDF")
                WebSet pbRender of oImageQualityCombo to (sNewValue = "PDF")
                WebSet pbRender of oPDFTypeCombo to (sNewValue = "PDF")
                WebSet pbRender of oFieldSeparatorForm to (sNewValue = "CSV")
                WebSet pbRender of oFieldDelimiterForm to (sNewValue = "CSV")
                WebSet pbRender of oExportGroupSectionsCheckbox to ("XLS|XLSX|CSV" contains sNewValue)
                WebSet pbRender of oExportPageSectionsCheckbox to ("XLS|XLSX|CSV" contains sNewValue)
                WebSet pbRender of oExportReportSectionsCheckbox to ("XLS|XLSX|CSV" contains sNewValue)
                WebSet pbRender of oNewSheetsCombo to ("XLS|XLSX" contains sNewValue)
                WebSet pbRender of oExportDataOnlyCheckbox to ("XLS|XLSX" contains sNewValue)
                WebSet pbRender of oExportWithFormattingCheckbox to ("XLS|XLSX" contains sNewValue)
                WebSet pbRender of oColumnWidthForm to ("XLS|XLSX" contains sNewValue)
            End_Procedure

            Procedure OnFill
                Handle hoView
                String sOption
                tNameValuePair[] ReportExportOptions

                Forward Send OnFill

                Get ViewObject to hoView
                WebGet pReportExportOptions of hoView to ReportExportOptions

                Get NamedValueGet ReportExportOptions C_$DRCSV to sOption
                If (sOption <> "0") Begin
                    Send AddComboItem "CSV" C_$DRCSV
                End

                Get NamedValueGet ReportExportOptions C_$DRHTML to sOption
                If (sOption <> "0") Begin
                    Send AddComboItem "HTML" C_$DRHTML
                End

                Get NamedValueGet ReportExportOptions C_$DRGIF to sOption
                If (sOption <> "0") Begin
                    Send AddComboItem "GIF" C_$DRGIF
                End

                Get NamedValueGet ReportExportOptions C_$DRJPEG to sOption
                If (sOption <> "0") Begin
                    Send AddComboItem "JPEG" C_$DRJPEG
                End

                Get NamedValueGet ReportExportOptions C_$DRPNG to sOption
                If (sOption <> "0") Begin
                    Send AddComboItem "PNG" C_$DRPNG
                End

                Get NamedValueGet ReportExportOptions C_$DRPDF to sOption
                If (sOption <> "0") Begin
                    Send AddComboItem "PDF" C_$DRPDF
                End

                Get NamedValueGet ReportExportOptions C_$DRRTF to sOption
                If (sOption <> "0") Begin
                    Send AddComboItem "RTF" C_$DRRTF
                End

                Get NamedValueGet ReportExportOptions C_$DRTIFF to sOption
                If (sOption <> "0") Begin
                    Send AddComboItem "TIFF" C_$DRTIFF
                End

                Get NamedValueGet ReportExportOptions C_$DRXLS2003 to sOption
                If (sOption <> "0") Begin
                    Send AddComboItem "XLS" C_$DRXLS2003
                End

                Get NamedValueGet ReportExportOptions C_$DRXLS2007 to sOption
                If (sOption <> "0") Begin
                    Send AddComboItem "XLSX" C_$DRXLS2007
                End
            End_Procedure
        End_Object

        Object oAllPagesCheckbox is a cWebCheckbox
            Set psLabel to C_$DRAllPages
            Set piColumnSpan to 1
            Set pbShowLabel to True
            Set pbServerOnChange to True
            Set peLabelAlign to alignRight

            Procedure OnChange String sNewValue String sOldValue
                WebSet pbEnabled of oPageNumberForm to (sNewValue = "0")
            End_Procedure
        End_Object

        Object oPageNumberForm is a cWebForm
            Set psLabel to C_$DRPageNumber
            Set piColumnSpan to 3
            Set peLabelAlign to alignRight
            Set piColumnIndex to 2
            Set piMaxLength to 6
            Set piPrecision to 0
        End_Object

        Object oDestinationCombo is a cWebCombo
            Set psLabel to C_$DRDestination
            Set peLabelAlign to alignRight
            Set piColumnSpan to 5

            Procedure OnFill
                Forward Send OnFill

                Send AddComboItem btCurrentWindow C_$DRCurrentWindow
                Send AddComboItem btNewWindow C_$DRNewWindow
                Send AddComboItem btNewTab C_$DRNewTab
            End_Procedure
        End_Object

        Object oPDFTypeCombo is a cWebCombo
            Set psLabel to C_$DRPDFType
            Set peLabelAlign to alignRight
            Set piColumnSpan to 5
            Set pbServerOnChange to True

            Procedure OnChange String sNewValue String sOldValue
                WebSet pbRender of oFacturXFileNameForm to (sNewValue = C_drFacturX)
            End_Procedure

            Procedure OnFill
                Forward Send OnFill

                Send AddComboItem C_drPDFNormal C_$DRPDFNormal
                Send AddComboItem C_drPDFA C_$DRPDFA
                Send AddComboItem C_drPDFA3 C_$DRPDFA3
                Send AddComboItem C_drFacturX C_$DRFacturX
            End_Procedure
        End_Object

        Object oFacturXFileNameForm is a cWebFileUploadForm
            Set psLabel to C_$DRFacturX
            Set piColumnSpan to 5
            Set peLabelAlign to alignRight
            Set psAccept to "application/xml,text/xml"

            { WebProperty = Server }
            Property String psFacturXFilePath

            Procedure OnFileFinished String sFileName String sLocalPath
                WebSet psFacturXFilePath to sLocalPath
            End_Procedure

            Function OnFileUpload String sFileName Integer iBytes String sMime Returns String
                String sUploadFolder sPath

                Get UploadFolder of ghoWebApp False to sUploadFolder
                If (sUploadFolder <> "") Begin
                    // When the folder is not present simply deny upload
                    If (Right (sUploadFolder, 1) <> SysConf (SYSCONF_DIR_SEPARATOR)) Begin
                        Move (sUploadFolder - SysConf (SYSCONF_DIR_SEPARATOR)) to sUploadFolder
                    End
                    Move (sUploadFolder - RandomHexUUID() - '-' - sFileName) to sPath
                End

                Function_Return sPath
            End_Function

            WebSetResponsive peLabelPosition rmMobilePortrait to lpTop
            WebSetResponsive peLabelAlign rmMobilePortrait to alignLeft
            WebSetResponsive psLabel rmMobilePortrait to C_$DRFacturX
            WebSetResponsive piColumnSpan rmMobilePortrait to 0
        End_Object

        Object oOwnerPasswordForm is a cWebForm
            Set psLabel to C_$DROwnerPassword
            Set piColumnSpan to 5
            Set peLabelAlign to alignRight
        End_Object

        Object oUserPasswordForm is a cWebForm
            Set psLabel to C_$DRUserPassword
            Set piColumnSpan to 6
            Set peLabelAlign to alignRight
            Set piColumnIndex to 5
        End_Object

        Object oPageModeCombo  is a cWebCombo
            Set psLabel to C_$DRPageMode
            Set peLabelAlign to alignRight
            Set piColumnSpan to 5

            Procedure OnFill
                Forward Send OnFill

                Send AddComboItem C_drNormal C_$DRNormal
                Send AddComboItem C_drOutline C_$DROutline
                Send AddComboItem C_drThumbs C_$DRThumbs
                Send AddComboItem C_drFullScreen C_$DRFullScreen
            End_Procedure
        End_Object

        Object oImageQualityCombo  is a cWebCombo
            Set psLabel to C_$DRImgQuality
            Set peLabelAlign to alignRight
            Set piColumnSpan to 5

            Procedure OnFill
                Forward Send OnFill

                Send AddComboItem C_drLow C_$DRLow
                Send AddComboItem C_drHigh C_$DRHigh
            End_Procedure
        End_Object

        Object oFieldSeparatorForm is a cWebForm
            Set psLabel to C_$DRSeparator
            Set piColumnSpan to 3
            Set peLabelAlign to alignRight
        End_Object

        Object oFieldDelimiterForm is a cWebForm
            Set psLabel to C_$DRDelimiter
            Set piColumnSpan to 3
            Set peLabelAlign to alignRight
            Set piColumnIndex to 3
        End_Object

        Object oExportGroupSectionsCheckbox is a cWebCheckbox
            Set psLabel to C_$DRExportGroupSections
            Set piColumnSpan to 3
            Set pbShowLabel to True
            Set peLabelAlign to alignRight
        End_Object

        Object oExportPageSectionsCheckbox is a cWebCheckbox
            Set psLabel to C_$DRExportPageSections
            Set piColumnSpan to 3
            Set pbShowLabel to True
            Set peLabelAlign to alignRight
            Set piColumnIndex to 3
        End_Object

        Object oExportReportSectionsCheckbox is a cWebCheckbox
            Set psLabel to C_$DRExportReportSections
            Set piColumnSpan to 3
            Set pbShowLabel to True
            Set peLabelAlign to alignRight
            Set piColumnIndex to 6
        End_Object

        Object oExportDataOnlyCheckbox is a cWebCheckbox
            Set psLabel to C_$DRExportDataOnly
            Set piColumnSpan to 3
            Set pbShowLabel to True
            Set peLabelAlign to alignRight
            Set pbServerOnChange to True
            Set piColumnIndex to 9

            Procedure OnChange String sNewValue String sOldValue
                WebSet pbEnabled of oColumnWidthForm to (sNewValue = "0")
            End_Procedure
        End_Object

        Object oExportWithFormattingCheckbox is a cWebCheckbox
            Set psLabel to C_$DRExportWithFormatting
            Set piColumnSpan to 1
            Set pbShowLabel to True
            Set peLabelAlign to alignRight
            Set pbServerOnChange to True
        End_Object

        Object oColumnWidthForm is a cWebForm
            Set psLabel to C_$DRExportColumnWidth
            Set piColumnSpan to 3
            Set peLabelAlign to alignRight
            Set peDataType to typeNumber
            Set piMaxLength to 4
            Set piPrecision to 0
        End_Object

        Object oNewSheetsCombo is a cWebCombo
            Set psLabel to C_$DRXLSNewSheets
            Set peLabelAlign to alignRight
            Set piColumnSpan to 5

            Procedure OnFill
                Forward Send OnFill

                Send AddComboItem C_drNoSheets C_$DRXLSNoSheets
                Send AddComboItem C_drSheetsPage C_$DRXLSSheetsPage
            End_Procedure
        End_Object

        Object oExportButton is a cWebButton
            Set psCaption to C_$DRExportReport
            Set piColumnSpan to 2

            Procedure OnClick
                Send Ok
            End_Procedure
        End_Object
    End_Object

    Function ExportChoices Returns tWebNavigateData
        String sExportFormat sValue
        Boolean bAllPages
        tWebNavigateData NavigateData

        WebGet psValue of oOutputCombo to sExportFormat
        Get NamedValueAdd NavigateData.NamedValues "ExportFormat" sExportFormat to NavigateData.NamedValues

        Get GetChecked of oAllPagesCheckbox to bAllPages
        Get NamedValueAdd NavigateData.NamedValues "AllPages" bAllPages to NavigateData.NamedValues

        If (not (bAllPages)) Begin
            WebGet psValue of oPageNumberForm to sValue
            Get NamedValueAdd NavigateData.NamedValues "PageNumber" sValue to NavigateData.NamedValues
        End

        WebGet psValue of oDestinationCombo to sValue
        Get NamedValueAdd NavigateData.NamedValues "Destination" sValue to NavigateData.NamedValues

        Case Begin
            Case (sExportFormat = "PDF")
                WebGet psValue of oOwnerPasswordForm to sValue
                Get NamedValueAdd NavigateData.NamedValues "OwnerPassword" sValue to NavigateData.NamedValues

                WebGet psValue of oUserPasswordForm to sValue
                Get NamedValueAdd NavigateData.NamedValues "UserPassword" sValue to NavigateData.NamedValues

                WebGet psValue of oPageModeCombo to sValue
                Get NamedValueAdd NavigateData.NamedValues "PageMode" sValue to NavigateData.NamedValues

                WebGet psValue of oImageQualityCombo to sValue
                Get NamedValueAdd NavigateData.NamedValues "ImageQuality" sValue to NavigateData.NamedValues

                WebGet psValue of oPDFTypeCombo to sValue
                Get NamedValueAdd NavigateData.NamedValues "PDFType" sValue to NavigateData.NamedValues
                If (sValue = C_drFacturX) Begin
                    WebGet psFacturXFilePath of oFacturXFileNameForm to sValue
                    If (sValue <> "") Begin
                        Get NamedValueAdd NavigateData.NamedValues "FacturXFileName" sValue to NavigateData.NamedValues
                    End
                End
                Case Break
            Case (sExportFormat = "XLS")
            Case (sExportFormat = "XLSX")
                Get GetChecked of oExportDataOnlyCheckbox to sValue
                Get NamedValueAdd NavigateData.NamedValues "DataOnly" sValue to NavigateData.NamedValues

                WebGet psValue of oColumnWidthForm to sValue
                Get NamedValueAdd NavigateData.NamedValues "ColumnWidth" sValue to NavigateData.NamedValues

                WebGet psValue of oNewSheetsCombo to sValue
                Get NamedValueAdd NavigateData.NamedValues "NewSheets" sValue to NavigateData.NamedValues

                Get GetChecked of oExportGroupSectionsCheckbox to sValue
                Get NamedValueAdd NavigateData.NamedValues "ExportGroupSections" sValue to NavigateData.NamedValues

                Get GetChecked of oExportPageSectionsCheckbox to sValue
                Get NamedValueAdd NavigateData.NamedValues "ExportPageSections" sValue to NavigateData.NamedValues

                Get GetChecked of oExportReportSectionsCheckbox to sValue
                Get NamedValueAdd NavigateData.NamedValues "ExportReportSections" sValue to NavigateData.NamedValues

                Get GetChecked of oExportWithFormattingCheckbox to sValue
                Get NamedValueAdd NavigateData.NamedValues "ExportWithFormatting" sValue to NavigateData.NamedValues
                Case Break
            Case (sExportFormat = "CSV")
                WebGet psValue of oFieldSeparatorForm to sValue
                Get NamedValueAdd NavigateData.NamedValues "FieldSeparator" sValue to NavigateData.NamedValues

                WebGet psValue of oFieldDelimiterForm to sValue
                Get NamedValueAdd NavigateData.NamedValues "FieldDelimiter" sValue to NavigateData.NamedValues

                Get GetChecked of oExportGroupSectionsCheckbox to sValue
                Get NamedValueAdd NavigateData.NamedValues "ExportGroupSections" sValue to NavigateData.NamedValues

                Get GetChecked of oExportPageSectionsCheckbox to sValue
                Get NamedValueAdd NavigateData.NamedValues "ExportPageSections" sValue to NavigateData.NamedValues

                Get GetChecked of oExportReportSectionsCheckbox to sValue
                Get NamedValueAdd NavigateData.NamedValues "ExportReportSections" sValue to NavigateData.NamedValues
                Case Break
        Case End

        Function_Return NavigateData
    End_Function

    Set pbServerOnShow to True

    Procedure OnShow
        String sExportType

        WebSet pbEnabled of oPageNumberForm to False
        Send SetChecked of oAllPagesCheckbox True
        WebSet psValue of oDestinationCombo to btNewWindow
        Send SetChecked of oExportDataOnlyCheckbox False
        WebSet pbEnabled of oColumnWidthForm to True
        WebSet psValue of oPDFTypeCombo to C_drPDFNormal

        WebSet pbRender of oFacturXFileNameForm to False

        WebSet psValue of oImageQualityCombo to C_drHigh
        WebSet psValue of oPageModeCombo to C_drNormal
        WebSet psValue of oFieldSeparatorForm to '"'
        WebSet psValue of oFieldDelimiterForm to ','

        Send SetChecked of oExportGroupSectionsCheckbox False
        Send SetChecked of oExportPageSectionsCheckbox False
        Send SetChecked of oExportReportSectionsCheckbox False
        Send SetChecked of oExportWithFormattingCheckbox False

        WebSet psValue of oNewSheetsCombo to C_drNoSheets

        WebSet psValue of oColumnWidthForm to 30
        Send SetChecked of oExportDataOnlyCheckbox False

        WebGet psValue of oOutputCombo to sExportType
        Send OnChange of oOutputCombo sExportType ""
    End_Procedure

    Procedure Popup Handle hoReturnObject
        Integer eDelegationMode eExportType

        Forward Send Popup hoReturnObject

        Get Delegation_Mode of hoReturnObject to eDelegationMode
        Set Delegation_Mode of hoReturnObject to No_Delegate_Or_Error
        Get ReportExportType of hoReturnObject to eExportType
        Set Delegation_Mode of hoReturnObject to eDelegationMode
        Case Begin
            Case (eExportType = C_drPDF)
                WebSet psValue of oOutputCombo to C_$DRPDF
                Case Break
            Case (eExportType = C_drCSV)
                WebSet psValue of oOutputCombo to C_$DRCSV
                Case Break
            Case (eExportType = C_drImage)
                WebSet psValue of oOutputCombo to C_$DRImage
                Case Break
            Case (eExportType = C_drHTML)
                WebSet psValue of oOutputCombo to C_$DRHTML
                Case Break
            Case (eExportType = C_drXLS)
                WebSet psValue of oOutputCombo to C_$DRXLS
                Case Break
            Case (eExportType = C_drRTF)
                WebSet psValue of oOutputCombo to C_$DRRTF
                Case Break
        Case End
    End_Procedure
End_Object
